PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
#SELECT DISTINCT ?class  (COUNT(?s) AS ?instanceTotal)
CONSTRUCT {
    ?class rdf:type owl:Class ;
           rdfs:subClassOf <http://data.sparna.fr/ontologies/sparnatural-config-core#SparnaturalClass> ;
           rdfs:label ?label .
}
WHERE {
    ?s a ?class .

    OPTIONAL { ?class rdfs:label ?theLabel . }
    BIND(IF(BOUND(?theLabel),
        ?theLabel,
        IF(CONTAINS(str(?class), '#'),
            STRAFTER(str(?class),"#"),
            STR(?class)
        )
    ) AS ?label) . 
    
    FILTER(?class NOT IN(
        rdf:List,
        rdf:Property,
        rdfs:Class,
        owl:Class,
        owl:SymmetricProperty,
        owl:TransitiveProperty,
        rdfs:Datatype,
        rdfs:ContainerMembershipProperty,
        owl:NamedIndividual,
        owl:AnnotationProperty,
        owl:ObjectProperty,
        owl:DatatypeProperty,
        owl:Ontology))
}
ORDER BY ?class